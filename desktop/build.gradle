import static org.gradle.api.file.DuplicatesStrategy.EXCLUDE

plugins {
    alias(libs.plugins.kotlin)
}

sourceSets.main {
    resources.srcDirs += "$rootDir/assets/"
}

dependencies {
    implementation libs.libGDX.backend.lwjgl3
    implementation libs.libGDX.platform, classifier("natives-desktop")
    implementation libs.libGDX.box2d.platform, classifier("natives-desktop")
    implementation libs.libGDX.freetype.platform, classifier("natives-desktop")

    implementation project(":core")
}

jar {
    archiveBaseName = "$rootProject.name"
    duplicatesStrategy(EXCLUDE)
    dependsOn configurations.runtimeClasspath
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }

    manifest {
        attributes([
            "Main-Class": "com.matthias.breakout.DesktopLauncherKt"
        ])
    }
}

def classifier(String value) {
    return { artifact { classifier = value } }
}